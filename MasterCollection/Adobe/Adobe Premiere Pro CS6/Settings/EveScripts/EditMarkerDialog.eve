layout my_dialog
{
	view dva_dialog(name: '$$$/Premiere/DLG/EditMarkerDialog/kResDLGEditMarkerDialogTitle=Edit Marker', identifier:'EditMarkerDialog')
	{
		row(child_horizontal: align_left, horizontal: align_left)
		{
			column(child_horizontal:align_left, horizontal: align_fill)
			{
				row(horizontal: align_fill)
				{
					dva_text_edit(identifier:'nameTextEdit', minWidth:250, minHeight:20, horizontal: align_fill, label:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGMarkerName=Name:');
				}
				row(horizontal: align_fill)
				{
					dva_static_text(identifier: 'timecodeStart', minWidth:90, minHeight:20, horizontal: align_left, label:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGMarkerTime=Time:');
					dva_subview(identifier: 'timecodeDuration', minWidth:90, minHeight:20, horizontal: align_left, label:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGMarkerDuration=Duration:');
				}
				row(horizontal: align_fill)
				{
					dva_text_edit(identifier:'descriptionTextEdit', minWidth:250, minHeight:100, multiLine:'true', vertical_scroll:'true', horizontal: align_fill, label:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGMarkerComments=Comments:');
				}
				dva_button(identifier:'customMarkerButton', name:'$$$/Premiere/DLG/Custom=Custom');
				row(horizontal: align_fill, guide_mask:[@guide_label])
				{
					dva_groupbox(name:'$$$/Premiere/DLG/EditMarkerDialogOptions=Options:', horizontal: align_fill)
					{
						column(horizontal: align_fill, child_horizontal: align_fill)
						{
							dva_radio_button(identifier:'commentRadio', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGCommentMarker=Comment Marker');
							dva_radio_button(identifier:'chapterMarkerRadio', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGEncoreMarkerChapter=Encore Chapter Marker');
							dva_radio_button(identifier:'webLinkRadio', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGWebLink=Web Link');
							dva_subview(identifier: 'webLinkStuff', horizontal:align_fill)
							{
								column(horizontal: align_fill)
								{
									dva_text_edit(identifier:'urlTextEdit', minWidth:220, minHeight:20, horizontal: align_fill, label:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGMarkerURL=URL:');
									dva_text_edit(identifier:'frameTargetTextEdit', minWidth:220, minHeight:20, horizontal: align_fill, label:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGMarkerFrameTarget=Frame Target:');
								}
							}
												
							dva_radio_button(identifier:'flashCueRadio', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGCuePoint=Flash Cue Point');
							dva_subview(identifier: 'flashStuff', horizontal:align_fill)
							{
								dva_radio_button_group(identifier: 'flashCueTypeRadioGroup', showBorder: 'false')
								{
									row()
									{
										dva_static_text(identifier:'flashCueTypeStatic', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGCuePointType=Type:');
										dva_radio_button(identifier:'flashCueEventRadio', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGEventCuePoint=Event', value: 'true');
										dva_radio_button(identifier:'flashCueNavigationRadio', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGnavigationCuePoint=Navigation');
									}
								}
								column(spacing:2, horizontal:align_fill)
								{
									dva_multi_column_tree(identifier:'cuePointParams', minHeight:120,  horizontal:align_fill);
									row(horizontal: align_right, spacing: 0)
									{
										dva_button(identifier:'addCuePointParam', maxWidth:10, name:'+');
										dva_button(identifier:'delCuePointParam', maxWidth:10, name:'-');
									}
								}
							}
						}
					}
				}
			}
			column(guide_mask:[@guide_baseline])
			{
				dva_ok_cancel_column(minWidth:80, minHeight:20);
				dva_button(name: '$$$/Premiere/DLG/EditMarkerDialog/kResDLGPrevious=Prev', identifier:'prevButton', minWidth:80, topSpace:10);
				dva_button(name: '$$$/Premiere/DLG/EditMarkerDialog/kResDLGNext=Next', identifier:'nextButton', minWidth:80);
				dva_button(name: '$$$/Premiere/DLG/EditMarkerDialog/kResDLGDelete=Delete', identifier:'delButton', minWidth:80, topSpace:10);
			}
		}
		row()
		{
			dva_static_image(identifier:'infoGraphic', minWidth: 40, minHeight: 30);
			dva_static_text(identifier:'markerOptionsNoteStaticText', name:'$$$/Premiere/DLG/EditMarkerDialog/kResDLGMarkerOptionsNote=Marker Options will only work with compatible output types.', horizontal:align_left, vertical:align_center);
		}
	}
} 
